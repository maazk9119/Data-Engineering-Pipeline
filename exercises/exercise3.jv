// Exercise # 3:
// Update Jayvee to version 0.6.4
// Build an automated data pipeline for the following source:
// Meta data: 
//  datacatalog.worldbank.org/search/dataset/0061114/World-Development-Report-2022---Chapter-5-Figures
// Direct link to data: 
//  https:thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx
//  Do NOT use a local file, always take the data from the link above
// Goal
// Extract the data we are interested in
// Extract the sheet called “Figure S5.1.2” from the Excel file
// Ignore the figure itself and only keep the table with actual data (from cell P2 to S45)
// Rename header columns: “ISO3” to "Country Code", “GDP per capita (US$, thousands)” to "GDP per Capita", “Share of government sustainable bonds" to "Bond Issuance Share"
// “Economy” does not have to be renamed
// Validate data, drop all invalid rows
//      "Country Code" must be a valid ISO 3166-1 alpha-3 country code (must not be empty)
//      "GDP per Capita" must be a positive decimal (must not be empty)
// "Bond Issuance Share" must be a decimal between 0 and 1 (both values included, must not be empty)
// Assign fitting built-in SQLite types (e.g., BIGINT, TEXT or FLOAT) to all columns
// Write data into a SQLite database called “country-stats.sqlite”
// One table “bondIssuance”, containing the data about bond issuance share (columns "Country Code" and "Bond Issuance Share")
// One table “gdpPerCapita”, containing the data about GDP per capita (columns "Country Code" and "GDP per Capita")
pipeline CountryStatePipeline{
    CountryStateExtractor
        -> CountryStateXLSXInterpreter
        -> CountryStateFigureS512Sheetpicker
        -> CountryStateDataSelector
        -> CountryStateNameHeaderWriter;

    CountryStateNameHeaderWriter    
        -> CountryStateBoundIssuanceTableInterpreter
        -> CountryStateBoundIssuanceLoader;
        
    CountryStateNameHeaderWriter
        -> CountryStateGdPerCapitaTableInterpreter
        -> CountryStateGdPerCapitaLoader;

    block CountryStateExtractor oftype HttpExtractor{
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block CountryStateXLSXInterpreter oftype XLSXInterpreter { }

    block CountryStateFigureS512Sheetpicker oftype SheetPicker {
        sheetName: 'Figure S5.1.2';
    }

    block CountryStateDataSelector oftype CellRangeSelector {
        select: range P2:S45;
    }

    block CountryStateNameHeaderWriter oftype CellWriter {
    at: range A1:D1;
    write: [
      "Country Code",
      "Economy",
      "GDP per Capita",
      "Bond Issuance Share"];
    }

    constraint BondIssuanceShareRegex oftype RangeConstraint {
        lowerBound: 0;
        upperBound: 1;
    }

    valuetype BondIssuanceShare oftype decimal {
        constraints: [
            BondIssuanceShareRegex,
        ];
    }

    constraint GdPerCapitaRegex oftype RangeConstraint {
        lowerBound: 0;
    }

    valuetype GdPerCapita oftype decimal {
        constraints: [
            GdPerCapitaRegex,
        ];
    }

    block CountryStateBoundIssuanceTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype BondIssuanceShare,
        ];
    }

    block CountryStateGdPerCapitaTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype GdPerCapita,
        ];
    }

    block CountryStateBoundIssuanceLoader oftype SQLiteLoader {
        table: 'bondIssuance';
        file: './country-stats.sqlite';
    }

    block CountryStateGdPerCapitaLoader oftype SQLiteLoader {
        table: 'gdpPerCapita';
        file: './country-stats.sqlite';
    }
}